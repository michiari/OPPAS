// formulae to verify here: mostly with THE CHAIN OPERATOR
probabilistic query: approximate;

program:
u4 fairness;
main() {
  bool res;
  fairness = 0u4 {1u4 : 10u4} 1u4 {1u4 : 10u4} 2u4 {1u4 : 10u4} 3u4 {1u4 : 10u4} 4u4 {1u4 : 10u4} 5u4 {1u4 : 10u4} 6u4 {1u4 : 10u4} 7u4 {1u4 : 10u4} 8u4 {1u4 : 10u4} 9u4;
  query fun(res);
  // res is which caf√® they have gone to
}

fun(bool &res) {
    bool x,y;
    query alice(x);
    query bob(y);
    observe x == y;
    res = x;
}

alice(bool &x) {
    bool prior, fair, bob_choice;
    // sample according to the prior (0.6)
    prior = 1u1 {6u4 : 10u4} 0u1;
    fair = 1u1 {fairness : 10u4} 0u1;
    if (fair) {
        query bob(bob_choice);
        observe prior == bob_choice;
    } else {}
    x = prior;
}

bob(bool &y) {
    bool prior, fair, alice_choice;
    // sample according to the prior (0.5)
    prior = 1u1 {5u4 : 10u4} 0u1;
    fair = 1u1 {fairness : 10u4} 0u1;
    if (fair) {
        query alice(alice_choice);
        observe prior == alice_choice;
    } else {}
    y = prior;
}
