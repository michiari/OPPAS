/* Original program: 

static int mid;
B(List<T> a, T val, int left, int right){
    // randomly select pivot element
    mid = Uniform(left,right);

    if (left < right){

        if (a[mid] < val){
            left = min(mid +1, right);
            B(a, val, left, right);
        } else {
            if (a[mid] > val) {
                right = max (mid -1, left);
                B(a, val, left, right);
            }
        }
    }
}
// B is supposed to store in mid the index of a where val lies 

Reference: F. Olmedo et al., Reasoning about Recursive Probabilistic Programs, LICS 2016.
*/

probabilistic query: quantitative;
// Q15: Partial Correctness
formula = G ((call And B And sorted And valOccurs And [B|left <= right]) --> XNu correctIndex);
//formula = F (ret And main And [|mid == 2s5]);

program:
u3[5] a;
u3 mid;
u3 val;
bool sorted, valOccurs, correctIndex;

main() {
    u3 left, right;
    u3 temp;
    s4 counter;

    // val = 1, left = 0, right = 2
    val = 2u3;
    left = 0u3;
    right = 4u3;

    // initialization
    counter = right;
    while (counter >= left){
        uniformSample(temp, 0u3, 4u3); // sample uniformly in [0,1,2,3,4];
        a[counter] = temp;
        counter = counter -1s4;
    }

    checkSortedness(left, right);
    checkValOccurrence(left, right);
    B(left, right);
    
}

B(u3 left, u3 right){
    uniformSample(mid, left, right);

    if (left < right){
        if (a[mid] < val){
            if (mid + 1s4 < right){
                left = mid + 1s4;
            } else {
                left = right;
            }
            checkSortedness(left, right);
            checkValOccurrence(left, right);
            B(left, right);
        } else {
            if (a[mid] > val) {
                if (mid - 1s4 > left){
                    right = mid - 1s4;
                } else {
                    right = left;
                }
                checkSortedness(left, right);
                checkValOccurrence(left, right);
                B(left, right);
            } else {}
        }
    } else {}
    checkCorrectIndex();

}

uniformSample(u3 &elem, u3 left, u3 right){
    u3 counter;
    bool stop;
    if (left > right){
        while (true) {
            // the random assignment diverges if left > right
            stop = false; // loops with empty body are not allowed in MiniProb.
        }
    } else {
        counter = right; 
        stop = false;
        while (counter > left && !stop){
            stop = true {1u3 : counter - left + 1u3} false;
            if (!stop){
                counter = counter - 1u3;
            } else {}
        }
        elem = counter;
    }  
}

checkSortedness(u3 left, u3 right){
    u3 counter;
    bool newSorted;
    newSorted = true;
    
    if (left < right){
        counter = right;
        while(counter > left){
            newSorted = newSorted && a[counter - 1u3] <= a[counter];
            counter = counter - 1u3;
        }
    } else{
        // if there is one or less elements, we assume sorted by default
    }
    sorted = newSorted;
    
}

checkValOccurrence(u3 left, u3 right){
    s4 counter;
    bool newValOccurs;
    newValOccurs = false;
    // 
    if (left <= right){
        counter = right;
        newValOccurs = false;
        while(counter >= left){
            newValOccurs = newValOccurs || a[counter] == val;
            counter = counter - 1s4;
        }
        
    } else{
       // val certainly does not occurr in an empty array 
    } 
    valOccurs = newValOccurs;
}

checkCorrectIndex(){
    correctIndex = a[mid] == val;
}

