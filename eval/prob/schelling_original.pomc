/* Original Church program (here modelled with parameter "depth" = 5):
( define ( sample-location )
    ( if ( flip .55)
        ' popular-bar
        ' unpopular-bar ) )

( define ( alice depth )
    ( query
        ( define alice-location ( sample-location ) )
        alice-location
        ( equal? alice-location ( bob (- depth 1) ) ) ) )

( define ( bob depth )
    ( query
        ( define bob-location ( sample-location ) )
        bob-location
        ( or (= depth 0)
            ( equal? bob-location ( alice depth ) ) ) ) )
Reference: A. Stuhlmueller, N.D. Goodman, Reasoning about Reasoning by Nested Conditioning: Modeling Theory of Mind with Probabilistic Programs, Cognitive Systems Research 2014.
*/ 


probabilistic query: qualitative;
formula = F (ret And main);

program:
u3 p;
main() {
  bool res;
  p = 5u3;
  query alice(res);
  // res is which cafÃ¨ they have gone to
}

alice(bool &x) {
    bool prior_alice, bob_choice;
    // sample according to the prior (0.55)
    prior_alice = 1u1 {11u5 : 20u5} 0u1;
    p = p - 1u3;
    query bob(bob_choice);
    observe prior_alice == bob_choice;
    x = prior_alice;
}

bob(bool &y) {
    bool prior_bob, alice_choice;
    // sample according to the prior (0.55)
    prior_bob = 1u1 {11u5 : 20u5} 0u1;
    if (p > 0u3) {
        query alice(alice_choice);
        observe prior_bob == alice_choice;
    } else {}
    y = prior_bob;
}
