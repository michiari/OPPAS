formulas = G ((call And Not ("main" Or "P_cp")) --> Not (T Ud (call And read))),
           G ((call And Not ("main" Or "P_db")) --> Not (T Ud (call And write))),
           G (call And (("account.canpay" And Not "P_cp") Or ("account.debit" And Not "P_db")) --> PNu exc Or XNu exc),
           Not (T Ud [|balance < 0s8]);

program:
bool P_cp, P_db;
s8 balance;

main() {
  balance = 120s8;

  P_cp = true;
  P_db = true;

  spender();

  P_cp = false;
  P_db = false;

  clyde();
}

spender() {
  s8 amount;
  bool auth;
  amount = *;

  account.canpay(amount, auth);
  if (auth) {
    account.debit(amount);
  } else {}
}

clyde() {
  s8 amount;
  amount = *;
  account.debit(amount);
}

account.canpay(s8 amount, bool &auth) {
  s8 bal;
  if (P_cp) {
    balance.read(bal);
    auth = bal - amount >= 0s8;
  } else {
    throw;
  }
}

account.debit(s8 amount) {
  bool auth;
  if (P_db) {
    account.canpay(amount, auth);
    if (auth) {
      balance.write(amount);
    } else { }
  } else {
    throw;
  }
}

balance.read(s8 &out) {
  out = balance;
}

balance.write(s8 amount) {
  balance = balance - amount;
}
